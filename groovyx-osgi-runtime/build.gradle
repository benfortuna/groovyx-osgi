apply plugin:'project-report'
apply plugin:'java'
apply plugin:'groovy'
apply plugin:'osgi'
apply plugin:'eclipse'
apply plugin:'maven'

defaultTasks 'build'

build.dependsOn 'javadoc'

// workaround for GRADLE-1079 (http://jira.codehaus.org/browse/GRADLE-1079)
eclipseClasspath.variables = [:]
eclipseClasspath.doFirst {
	downloadSources = true
	downloadJavadoc = true
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
	classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: compileJava) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

artifacts {
	archives javadocJar
	archives sourcesJar
}


uploadArchives.doFirst {
	repositoryDir.mkdirs()
}
uploadArchives {
	repositories.mavenDeployer {
		repository(url: repositoryDir.toURL().toString())
		pom.project {
			licenses {
				license {
					name 'The Apache Software License, Version 2.0'
					url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
		}
	}
}

dependencies {
	compile group: 'org.osgi', name: 'org.osgi.core', version: "$osgiVersion"
	compile group: 'org.osgi', name: 'org.osgi.compendium', version: "$osgiVersion"
	compile group: 'org.eclipse.osgi', name:'org.eclipse.osgi', version:"$equinoxVersion"
	compile group: 'org.apache.felix', name:'org.apache.felix.framework', version:"$felixVersion"
	compile group: 'org.apache.commons', name: 'com.springsource.org.apache.commons.logging', version: "$jclVersion"
	groovy (group: 'org.codehaus.groovy', name: 'groovy', version: "$groovyVersion") {
		//transitive = false
		exclude module: 'jansi'
	}                                                                 
	compile group: 'org.apache.ivy', name: 'ivy', version: "$ivyVersion"
	testCompile group: 'junit', name: 'junit', version: "$junitVersion"
}

jar {
	baseName = 'groovyx.osgi.runtime'
	manifest {
		//symbolicName = 'groovyx.osgi.runtime'
		instruction 'Export-Package', 'groovyx.osgi.runtime.*'
		instruction 'Import-Package', '*'
		instruction 'Bundle-Vendor', 'JetztGrad.net'
	}
}
